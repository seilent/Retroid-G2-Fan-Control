plugins {
    id 'com.android.application'
}

android {
    namespace 'com.seilent.rpfanctl'
    compileSdk 35

    defaultConfig {
        applicationId "com.seilent.rpfanctl"
        minSdk 28
        targetSdk 35
        versionCode 1
        versionName "1.0.0"
    }

    signingConfigs {
        release {
            def keystoreFile = System.getenv("KEYSTORE_FILE")
            if (keystoreFile != null) {
                storeFile file(keystoreFile)
            } else {
                def keystorePath = file("rpfanctl-release.keystore")
                if (keystorePath.exists()) {
                    storeFile keystorePath
                } else {
                    storeFile file("dummy.keystore")
                }
            }
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: System.getProperty("KEYSTORE_PASSWORD", "")
            keyAlias System.getenv("KEY_ALIAS") ?: System.getProperty("KEY_ALIAS", "rpfanctl")
            keyPassword System.getenv("KEY_PASSWORD") ?: System.getProperty("KEY_PASSWORD", "")
        }
    }

    buildTypes {
        release {
            def keystoreFile = System.getenv("KEYSTORE_FILE")
            def keystorePath = file("rpfanctl-release.keystore")
            if (keystoreFile != null || keystorePath.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
}
